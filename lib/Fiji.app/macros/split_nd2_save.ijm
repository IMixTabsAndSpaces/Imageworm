macro 'convert ND2 tiff' {

/*
 * - converts ND2 or ome.tiff files into separate TIFF. 
 * - fornow manualy change the script output and input path.
 * - each series is saved to a new directory of tiff. The name of the series is appended to the file name and directory.
 * - the TIFFs will be scaled becuse they are in 12 bit format but computers stores them in 16 bit and we want 8 bit.
 * - the TIFFs are saved in a folder generated by the macro
 * 
 * stephen Nehrbass, Oct 2018
 * 
 */

output = "/mnt/Disk_2/work/images/output/";
path ="/mnt/Disk_2/work/images/ND2_Files/ChannelGFP,TxRed_Seq0000.nd2";

run("Bio-Formats Macro Extensions");
Ext.setId(path);
Ext.getCurrentFile(file);
Ext.getSeriesCount(seriesCount);

getDateAndTime(year, month, dayOfWeek, dayOfMonth, hour, minute, second, msec);
month += 1;
date = d2s(year,0)+"-"+d2s(month,0)+"-"+d2s(dayOfMonth,0)+"-"+d2s(hour,0)+":"+d2s(minute,0);
print(date);

setBatchMode(true);
for (s=1; s<=seriesCount; s++){
    run("Bio-Formats Importer", "open="+path+" autoscale color_mode=Default view=Hyperstack stack_order=XYCZT series_"+s);	
    t=getTitle();
    run("Stack Slicer", "split_channels stack_order=XYCZT");
    
    list = getList("image.titles");
    
    date = substring(t, 0, 10);
    sub_output = output + date + s;
    File.makeDirectory(sub_output);
    chdirs = newArray(sub_output+"/tif", sub_output+"/tifR");
    File.makeDirectory(chdirs[0]);
    File.makeDirectory(chdirs[1]);

    for (i=0; i<=1; i++)
    	{
	selectWindow(list[i]);
	//Stack.setChannel(i);
    	resetMinAndMax();
	//run("Grays");
	run("Apply LUT", "stack");
    	run("8-bit");
    	run("Image Sequence... ", "format=TIFF save=" + chdirs[i] + "/ name=" + date + s);
    }
    run ("Close All");
}

run("Close All");
setBatchMode(false);

} // macro


